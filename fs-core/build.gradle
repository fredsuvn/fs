plugins {
  id 'fs-build'
  id 'fs-build-repo'
  id 'java-library'
  id 'fs-build-test'
  id 'com.google.protobuf' version "$protobufPluginVersion"
  id 'fs-build-publish'
}

description = 'Fs core.'

java {
  registerFeature('proxySpringSupport') {
    usingSourceSet(sourceSets.main)
    capability('xyz.fsgik.build', 'proxy-spring-support', projectVersion)
  }
  registerFeature('proxyCglibSupport') {
    usingSourceSet(sourceSets.main)
    capability('xyz.fsgik.build', 'proxy-cglib-support', projectVersion)
  }
  registerFeature('dataProtobufSupport') {
    usingSourceSet(sourceSets.main)
    capability('xyz.fsgik.build', 'data-protobuf-support', projectVersion)
  }
}

dependencies {

  implementation platform(project(':fs-dependencies'))

  api project(':fs-annotations')

  compileOnly 'org.projectlombok:lombok:1.18.26'
  annotationProcessor 'org.projectlombok:lombok:1.18.26'

  proxySpringSupportImplementation 'org.springframework:spring-core'
  proxyCglibSupportImplementation 'cglib:cglib'
  dataProtobufSupportImplementation 'com.google.protobuf:protobuf-java'

  testImplementation project(":fs-test")
  testImplementation "com.google.protobuf:protobuf-java"

  testAnnotationProcessor platform(project(":fs-dependencies"))
  testAnnotationProcessor "org.openjdk.jmh:jmh-generator-annprocess"
}

def generatedPath = "$project.projectDir/generated"
def protoPath = "$generatedPath/proto"

sourceSets {
  test {
    proto {
      srcDirs = ["src/test/proto"]
    }
    java {
      srcDirs += "$protoPath/test/java"
    }
  }
}

protobuf {
  generatedFilesBaseDir = "$protoPath"
  protoc {
    // Download from repositories
    artifact = "com.google.protobuf:protoc:$protocVersion"
    generatedFilesBaseDir = "$protoPath"
  }

  plugins {
    //grpc {
    //  artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
    //}
    //grpckt {
    //  artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpcKotlinVersion"
    //}
  }

  generateProtoTasks {
    all()*.each { task ->
      task.plugins {
        // Generate Java gRPC classes
        //grpc {
        //  setOutputSubDir "$protoGenDir"
        //}
        // Generate Kotlin gRPC using the custom plugin from library
        //grpckt {}
      }
    }
  }
}

clean {
  delete("${protobuf.generatedFilesBaseDir}")
  delete("$generatedPath/temp")
}

javadoc {
  options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.register('sourceJar', Jar) {
  from(sourceSets.main.allSource)
  archiveClassifier.set('sources')
}

tasks.register('javadocJar', Jar) {
  dependsOn javadoc
  classifier 'javadoc'
  from javadoc.destinationDir
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact sourceJar
      artifact javadocJar
      configurePublishMavenPom(it, projectInfo)
    }
  }
//  repositories {
//    configurePublishRepositories(it, publishInfo)
//  }
//  configureSigning()
//  signing {
//    sign publishing.publications.maven
//  }
}