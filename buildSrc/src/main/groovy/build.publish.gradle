plugins {
  id 'build.core'
  id 'maven-publish'
  id 'signing'
}

def configurePublishMavenPom(MavenPublication maven) {
  maven.pom {
    name = project.name
    description = project.description
    url = configs.url
    inceptionYear = configs.inceptionYear
    scm {
      connection = "scm:git:${configs.url}.git"
      developerConnection = "scm:git:${configs.url}.git"
      url = configs.url
    }
    if (configs.licenses != null) {
      licenses {
        configs.licenses.forEach { each ->
          license {
            name = each.name
            url = each.url
          }
        }
      }
    }
    if (configs.developers != null) {
      developers {
        configs.developers.forEach { each ->
          developer {
            email = each.email
          }
        }
      }
    }
    fs.debug("maven-pom.name: ${name.get()}")
    fs.debug("maven-pom.description: ${description.get()}")
  }
}

def configurePublishRepositories(RepositoryHandler repositories) {
  if (configs.publish.isSnapshot) {
    repositories.maven {
      name = configs.publish.snapshotId
      url = configs.publish.urls.snapshot
      fs.debug("snapshot.name: $name")
      fs.debug("snapshot.url: $url")
      credentials {
        username = getProperty("publish${name.capitalize()}Username")
        password = getProperty("publish${name.capitalize()}Password")
        fs.debug("snapshot.username: $username")
        fs.debug("snapshot.password: $password")
      }
    }
  } else {
    repositories.maven {
      name = configs.publish.releaseId
      url = configs.publish.urls.release
      fs.debug("release.name: $name")
      fs.debug("release.url: $url")
      credentials {
        username = getProperty("publish${name.capitalize()}Username")
        password = getProperty("publish${name.capitalize()}Password")
        fs.debug("release.username: $username")
        fs.debug("release.password: $password")
      }
    }
  }
}

def configureSigning() {
  signing {
    def signingName = configs.publish.signingId
    def signingKeyId = getProperty("signing${signingName.capitalize()}KeyId")
    def signingPassword = getProperty("signing${signingName.capitalize()}Password")
    def signingKey = utils.readFileAsString(getProperty("signing${signingName.capitalize()}KeyFile"), "UTF-8")
    fs.debug("signing.signingKeyId: $signingKeyId")
    fs.debug("signing.signingPassword: $signingPassword")
    fs.debug("signing.signingKey: $signingKey")
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
  }
}

ext["configurePublishMavenPom"] = this.&configurePublishMavenPom
ext["configurePublishRepositories"] = this.&configurePublishRepositories
ext["configureSigning"] = this.&configureSigning