apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            pom {
                name = project.name
                description = project.description
                url = config.url
                licenses {
                    config.licenses.forEach { each ->
                        license {
                            name = each.name
                            url = each.url
                        }
                    }
                }
                developers {
                    config.developers.forEach { each ->
                        developer {
                            email = each.email
                        }
                    }
                }
                scm {
                    connection = "scm:git:${config.url}.git"
                    developerConnection = "scm:git:${config.url}.git"
                    url = config.url
                }
            }
        }
    }

    repositories {
        mavenLocal()

        maven {
            name = "sonatype"
            url = config.oss.staging
            credentials {
                username = "${tokens.sonatypeUserName}"
                password = "${tokens.sonatypePassword}"
            }
        }
        maven {
            name = "sonatypeSnapshot"
            url = config.oss.snapshot
            credentials {
                username = "${tokens.sonatypeUserName}"
                password = "${tokens.sonatypePassword}"
            }
        }
    }

    if (!isSnapshot) {
        signing {
            sign publishing.publications.maven
        }
    }
}